PREFIX instans: <http://instans.org/extensions/instans#>
PREFIX rdf:    <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX :       <http://www.w3.org/2009/sparql/docs/tests/data-sparql11/bind/manifest#>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mf:     <http://www.w3.org/2001/sw/DataAccess/tests/test-manifest#>
PREFIX qt:     <http://www.w3.org/2001/sw/DataAccess/tests/test-query#>
PREFIX dawgt:  <http://www.w3.org/2001/sw/DataAccess/tests/test-dawg#>

SELECT ?queryfile ?testtype ?parsed_ok ?translate_ok ?error_msg ?status WHERE {
       ?test a ?testtype; mf:name ?name .
       {
         { ?test mf:action [ qt:query ?queryfile ] } UNION { ?test mf:action ?queryfile }
	 FILTER(!isblank(?queryfile) && (strends(str(?queryfile), ".rq") || strends(str(?queryfile), ".ru")))
       }
       ?x rdf:first ?test .
       BIND(iri(strbefore(str(?queryfile),".")) AS ?instans)
       BIND(instans:add_rules(?instans, ?queryfile) AS ?add)
       BIND(instans:has_status(?instans, "instans-rule-parsing-succeeded") AS ?parsed_ok) 
       BIND(instans:has_status(?instans, "instans-rule-translation-succeeded") AS ?translate_ok) 
       BIND(instans:status(?instans) AS ?status) 
       BIND(instans:error_message(?instans) AS ?error_msg)
}
