BASE <http://instans.org/>
PREFIX : <http:instans.org/default#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX ssn: <http://purl.oclc.org/NET/ssnx/ssn#>
PREFIX ep: <http://www.ontologydesignpatterns.org/cp/owl/eventprocessing.owl#>
PREFIX omgeo: <http://www.ontotext.com/owlim/geo#>
PREFIX ff: <http://factforge.net/>

# Stateless filter (EPA 1): Only events generated during business hours

# @name EPA1-Stateless
CONSTRUCT { GRAPH <poststateless> {  # Forward outgoing events
    ?event a ep:EventObject ;    # Match incoming events
    	   geo:Point [ geo:lat ?lat ; geo:long ?long ] ;
	   ssn:Sensor ?sensor ;
	   ep:hasEventObjectSamplingTime ?time .
    }
}
WHERE { GRAPH <source> {
    ?event a ep:EventObject ;    # Match incoming events
    	   geo:Point [ geo:lat ?lat ; geo:long ?long ] ;
	   ssn:Sensor ?sensor ;
	   ep:hasEventObjectSamplingTime ?time .
    FILTER ( ( HOURS(?time) > 8 ) && ( HOURS(?time) < 17 ) ) }
};

# Cleanup <source> when a newer event is available

# @name Cleanup-Source
DELETE WHERE {
  GRAPH <source> {
    ?event a ep:EventObject ;    # Match incoming events
    	   geo:Point ?point ;
	   ssn:Sensor ?sensor ;
	   ep:hasEventObjectSamplingTime ?time .
    ?point geo:lat ?lat ; geo:long ?long
  }
};
