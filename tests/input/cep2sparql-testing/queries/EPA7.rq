BASE <http://instans.org/>
PREFIX : <http:instans.org/default#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX ssn: <http://purl.oclc.org/NET/ssnx/ssn#>
PREFIX ep: <http://www.ontologydesignpatterns.org/cp/owl/eventprocessing.owl#>
PREFIX omgeo: <http://www.ontotext.com/owlim/geo#>
PREFIX ff: <http://factforge.net/>

# Compose (EPA 7): Combine two event streams into one

# @name EPA7-Compose
DELETE { GRAPH <geoevents> { # Clean incoming events after processing
    ?event a ep:EventObject ;
           geo:Point ?point .
    ?point geo:lat ?lat ; geo:long ?long }
  GRAPH <timeevents> {
    ?event a ep:EventObject ;
           ssn:Sensor ?sensor ;
           ep:hasEventObjectSamplingTime ?time } }
INSERT { GRAPH <combined> {
    ?event a ep:EventObject ; # Forward re-combined events
           ssn:Sensor ?sensor ;
           geo:Point [ geo:lat ?lat ; geo:long ?long ; ] ;
           ep:hasEventObjectSamplingTime ?time } }
WHERE { 
GRAPH <geoevents> { # Match GeoEvents
    ?event a ep:EventObject ;
           geo:Point ?point .
    ?point geo:lat ?lat ; geo:long ?long }
  GRAPH <timeevents> { # Match TimeEvents
    ?event a ep:EventObject ;
           ssn:Sensor ?sensor ;
           ep:hasEventObjectSamplingTime ?time } } ;