PREFIX instans: <http://instans.org/extensions/instans#>
PREFIX rdf:    <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:   <http://www.w3.org/2000/01/rdf-schema#>
PREFIX mf:     <http://www.w3.org/2001/sw/DataAccess/tests/test-manifest#>
PREFIX qt:     <http://www.w3.org/2001/sw/DataAccess/tests/test-query#>
PREFIX ut:     <http://www.w3.org/2009/sparql/tests/test-update#>
PREFIX dawgt:  <http://www.w3.org/2001/sw/DataAccess/tests/test-dawg#>

#SELECT ?queryfile ?test { # ?testtype ?parsed_ok ?translate_ok ?error_msg ?status WHERE {
SELECT DISTINCT ?type ?negative ?suite ?collection ?name ?queryfile ?datafile ?graphdatafile ?resultfile ?resultgraphfile ?resultgraphlabel
WHERE {
   ?test a ?typelong; mf:name ?prettyname .
   BIND(strafter(str(?typelong),"#") AS ?type)
   BIND((?type = "NegativeSyntaxTest" || ?type = "NegativeSyntaxTest11" || ?type = "NegativeUpdateSyntaxTest11") AS ?negative)
   BIND(strafter(str(?test), "tests/") AS ?ts)
   BIND(strbefore(?ts, "/") AS ?suite)
   BIND(strbefore(strafter(?ts, "/"), "/") AS ?collection)
   BIND(concat("tests/", ?suite, "/", ?collection, "/") AS ?itss) 
   BIND(strafter(strafter(str(?test), ?itss), "manifest#") AS ?name)
#   { SELECT * {
   OPTIONAL { ?test mf:action ?a1 .
              ?a1 qt:query ?q1
	      OPTIONAL { ?a1 qt:data ?d1 }
	      OPTIONAL { ?a1 qt:graphData ?gd1 }}
   OPTIONAL { ?test mf:action ?a2 .
              ?a2 ut:request ?q2
	      OPTIONAL { ?a2 ut:data ?d2
	                 OPTIONAL { ?a2 ut:graphData ?gd2 ; rdfs:label ?graphlabel }}}
#			 } }
   OPTIONAL { ?test mf:action ?q3 FILTER(!isblank(?q3)) }
   BIND(coalesce(?q1, ?q2, ?q3) AS ?q)
   FILTER(bound(?q))

   BIND(strafter(str(?q), ?itss) AS ?queryfile)
   BIND(coalesce(?d1, ?d2) AS ?d)
   BIND(strafter(str(?d), ?itss) AS ?datafile)
   BIND(coalesce(?gd1, ?gd2) AS ?gdf)
   BIND(strafter(str(?gdf), ?itss) AS ?graphdatafile)
   OPTIONAL { ?test mf:result ?r1 .
              FILTER(isblank(?r1))
              OPTIONAL { ?r1 ut:data ?rd1 }
	      OPTIONAL { ?r1 ut:graphData [ ut:graph ?rg; rdfs:label ?rl ] } }
   OPTIONAL { ?test mf:result ?rd2 FILTER(!isblank(?rd2)) }
   BIND(coalesce(?rd1, ?rd2) AS ?rd)
   BIND(strafter(str(?rd), ?itss) AS ?resultfile)
   BIND(strafter(str(?rg), ?itss) AS ?resultgraphfile)
   BIND(?rl AS ?resultgraphlabel)
}
