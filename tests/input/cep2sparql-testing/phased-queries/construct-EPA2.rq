BASE <http://instans.org/>
PREFIX : <http:instans.org/default#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX ssn: <http://purl.oclc.org/NET/ssnx/ssn#>
PREFIX ep: <http://www.ontologydesignpatterns.org/cp/owl/eventprocessing.owl#>
PREFIX omgeo: <http://www.ontotext.com/owlim/geo#>
PREFIX ff: <http://factforge.net/>

# Stateful filter (EPA 2): Only 1 event per hour

# @name EPA2-Stateful-update-memory
DELETE { GRAPH <memory> { # Clear memory
    :stateful :prevHour ?prevhour; :prevEvent ?prevEvent} }
INSERT { GRAPH <memory> { # Write new value to memory
           :stateful :prevHour ?hour; :prevEvent ?event }  
}
WHERE {
      GRAPH <poststateless> { ?event ep:hasEventObjectSamplingTime ?time }
      GRAPH <memory> { OPTIONAL  { ?mem :prevHour ?prevhour; :prevEvent ?prevEvent } }
      BIND ( HOURS(?time) as ?hour)
      FILTER ( ?hour != COALESCE(?prevhour,-1) ) # Compare current hour with memory
} ;

# @name EPA2-Stateful-pass-one-event-per-hour
CONSTRUCT { GRAPH <poststateful> { ?s ?p ?o }}
WHERE {
      GRAPH <memory> { :stateful :prevEvent ?event }
      GRAPH <poststateless> {
      	    ?event a ep:EventObject .
	    ?s ?p ?o
      }
}
