;;; -*- Mode: Lisp; Syntax: COMMON-LISP; Base: 10; Package: INSTANS -*-
;;;
;;; Author: Esko Nuutila (esko.nuutila@aalto.fi)
;;;

(in-package #:instans)

(defgeneric store-count (memory)
  (:method ((this memory))
    (let ((store (memory-store this)))
      (cond ((null store) 0)
	    (t (hash-table-count (memory-store this)))))))

(defgeneric inner-token (memory token)
  (:method ((this existence-start-node) token) (cdddr token))
  (:method ((this filter-memory) token) (cddr token))
  (:method ((this memory) token) token))

(defgeneric store-get-token (memory token)
  (:method ((this memory) token)
    (let* ((key (second (car token)))
	   (stored-token (gethash key (memory-store this)))
	   (stored-token-inner (when-checkit (inner-token this stored-token)))
	   )
      (declare (ignorable stored-token-inner))
      (checkit (numberp key) "key ~S not a number" key)
      (checkit (or (null stored-token) (token-equal token stored-token-inner)) "token~%~A and inner-token~%~A not equal~%~A" token stored-token-inner stored-token)
      stored-token)))

(defgeneric store-put-token (memory token)
  (:method ((this memory) token)
    (let ((key (second (car token)))
	  (count (when-checkit (store-count this))))
      (declare (ignorable count))
      (checkit (numberp key) "key ~S not a number" key)
      (checkit (not (gethash key (memory-store this))) "token ~S already in memory ~S" token this)
      (setf (gethash key (memory-store this)) token)
      (checkit (= (1+ count) (store-count this)) "store-put-token did not increase the element count")
      (incf (memory-store-put-count this)))))

(defgeneric store-remove-token (memory token)
  (:method ((this memory) token)
    (let ((key (second (car token)))
	  (count (when-checkit (store-count this))))
      (declare (ignorable count))
      (checkit (numberp key) "key ~S not a number" key)
      (checkit (gethash key (memory-store this)) "token ~S not in memory ~S" token this)
      (remhash key (memory-store this))
      (checkit (= (1- count) (store-count this)) "store-remove-token did not decrease the element count")
      (incf (memory-store-remove-count this)))))

(defgeneric store-tokens (memory)
  (:method ((memory memory))
    (maph #'(lambda (k v)
	      (declare (ignore k))
	      v)
	  (memory-store memory))))

(defgeneric store-clear (memory)
  (:method ((memory memory))
    (let ((table (memory-store memory)))
      (unless (null table)
	(clrhash table)))))
